apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

String gitVersion() {
    ByteArrayOutputStream out = new ByteArrayOutputStream()
    exec {
        commandLine = [ 'git', 'describe', '--always', '--dirty' ]
        standardOutput = out
    }
    return out.toString().replace(System.lineSeparator(), '')
}

group = 'io.kamax'
version = gitVersion()

sourceCompatibility = 1.7
targetCompatibility = 1.8

String versionInfoFile = "${projectDir}/build/tmp/versionInfo/server.build.properties"

repositories {
    maven { url "https://maven.kamax.io/releases/" }
    maven { url "https://maven.kamax.io/snapshots/" }
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile project(':hbox-server-api')

    compile 'commons-daemon:commons-daemon:1.0.15'
    compile 'com.h2database:h2:1.3.174'

    testCompile 'junit:junit:4.12'
}

task versionInfo {
    doLast {
        File versionFile = new File(versionInfoFile)
        versionFile.getParentFile().mkdir()
        versionFile.text = 'version=' + project.version
    }
}

jar {
    dependsOn versionInfo
    from versionInfoFile
}
